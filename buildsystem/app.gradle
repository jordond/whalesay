apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion androidTargetSdkVersion

    defaultConfig {
        applicationId appApplicationId

        minSdkVersion androidMinimumSdkVersion
        targetSdkVersion androidTargetSdkVersion

        versionCode applicationVersionCode
        versionName applicationVersionName

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile = rootProject.file("debug.keystore")
            storePassword = "android"
            keyAlias = "debugkey"
            keyPassword = "android"
        }
        release {
            storeFile = rootProject.file("whalesay.keystore")
            storePassword = System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias = System.getenv("ANDROID_KEYSTORE_ALIAS")
            keyPassword = System.getenv("ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }
    }

    packagingOptions {
        exclude 'META-INF/core-ktx_release.kotlin_module'
    }

    buildTypes {
        debug {
            versionNameSuffix "-$suffixDebug"
            applicationIdSuffix ".$suffixDebug"
            debuggable true
            signingConfig signingConfigs.debug
        }
        beta {
            initWith release
            versionNameSuffix "-$suffixBeta"
            applicationIdSuffix ".$suffixBeta"
            signingConfig signingConfigs.release

            matchingFallbacks = ['debug']
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "${rootDir}/proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    kapt {
        javacOptions {
            // Increase the number of errors to show, stops Data-binding errors from hiding errors
            option("-Xmaxerrs", 500)
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* Kotlin */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutines"

    /* Architecture components */
    implementation "androidx.lifecycle:lifecycle-extensions:$archLifecycle"
    implementation "android.arch.navigation:navigation-fragment-ktx:$archNavigation"
    implementation "android.arch.navigation:navigation-ui-ktx:$archNavigation"

    /* Android libraries */
    implementation "androidx.constraintlayout:constraintlayout:$androidConstraintLayout"
    implementation "androidx.legacy:legacy-support-v4:$androidLegacy"
    implementation "androidx.appcompat:appcompat:$androidAppcompat"
    implementation "com.google.android.material:material:$androidMaterial"

    /* Ktx */
    implementation "androidx.core:core-ktx:$ktxCore"
    implementation "com.shopify:livedata-ktx:$ktxLiveData"
    implementation "com.snakydesign.livedataextensions:lives:$ktxLives"
    implementation "com.github.ajalt:timberkt:$ktxTimber"

    /* Dependency Injection */
    implementation "com.google.dagger:dagger:$diDagger"
    implementation "com.google.dagger:dagger-android:$diDagger"
    implementation "com.google.dagger:dagger-android-support:$diDagger"
    kapt "com.google.dagger:dagger-compiler:$diDagger"
    kapt "com.google.dagger:dagger-android-processor:$diDagger"

    /* Shared Preferences */
    implementation "com.chibatching.kotpref:kotpref:$prefsKotpref"
    implementation "com.chibatching.kotpref:initializer:$prefsKotpref"

    /* UI */
    implementation "com.github.etiennelenhart:Eiffel:$uiEiffel"
    implementation "com.xwray:groupie:$uiGroupie"
    implementation "com.xwray:groupie-databinding:$uiGroupie"
    implementation "com.xwray:groupie-kotlin-android-extensions:$uiGroupie"

    /* Network */
    implementation "com.squareup.retrofit2:retrofit:$networkRetrofit"
    implementation "com.squareup.moshi:moshi:$networkMoshi"

    /* Misc */
    implementation "joda-time:joda-time:$miscJodaTime"
    implementation "com.github.quickpermissions:quickpermissions-kotlin:$miscQuickPermissions"

    /* Debug Utils */
    implementation "com.jakewharton.timber:timber:$debugTimber"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$debugLeakCanary"
    debugImplementation "com.facebook.stetho:stetho:$debugStetho"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$debugStetho"

    /* Hyperion */
    debugImplementation "com.willowtreeapps.hyperion:hyperion-core:$debugHyperion"
    releaseImplementation "com.willowtreeapps.hyperion:hyperion-core-no-op:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-attr:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-measurement:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-disk:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-recorder:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-phoenix:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-crash:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-shared-preferences:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-geiger-counter:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-timber:$debugHyperion"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-build-config:$debugHyperion"

    /* Testing */
    testImplementation "junit:junit:$testJunit"
    testImplementation "androidx.arch.core:core-testing:$testAndroidArch"
    testImplementation "io.mockk:mockk:$testMockk"
    testImplementation "com.willowtreeapps.assertk:assertk:$testAssertK"
    testImplementation "org.robolectric:robolectric:$testRobolectric"
    androidTestImplementation "androidx.test:runner:$testAndroidRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testEspresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$testEspresso"
}
